#!/usr/bin/env perl

use strict;
use warnings FATAL => 'all';
use autodie;
use WebService::Simple;
use Data::Dumper;
use Getopt::Long;
use File::Slurp;
use Config::Pit;
use Pod::Usage;
use HTTP::Date;

our %opts;
GetOptions(\%opts,'code=s','password=s','id=s','user=s','lesson=s','tabwidth=i','lang=s','help!') or pod2usage(2);
pod2usage(1) if($opts{help});

my $config = pit_get("aoj");
# validate();

#sourceCode
my $dir = $opts{code} || $ARGV[0] || "";
(-f $dir) or die "Error: Couldn't find your code.";
my $code = read_file($dir);
my $tabwidth = $opts{tabwidth} || $config->{tabwidth} || 4;
my $tab2space = sprintf(" " x $tabwidth);
$code =~ s/\t/$tab2space/g;

#password
my $password = $opts{password} || $config->{password} || "";

#user_id
my $user = $opts{user} || $config->{user} || "";

#problem no
exists($opts{id}) or die "Input problem NO.";
my $id = $opts{id};
#$id = sprintf("%04d",$id);

#lesson
my $lesson_id = $opts{lesson} || "";

#lang
my $lang = $opts{lang} || $config->{lang};

my $aoj = WebService::Simple->new(
    base_url => "http://judge.u-aizu.ac.jp/",
    );

my $post_response = $aoj->post('onlinejudge/webservice/submit',{userID => $user,
							     password => $password,
							     problemNO => $id,
							     lessonID => $lesson_id,
							     language => $lang,
							     sourceCode => $code});

if($post_response->{_content} =~ /wrong/i){
    $post_response->{_content} =~ s/\n//g;
    $post_response->{_content} =~ s/<.+?>//g;
    print $post_response->{_content},"\n";
    die;
}

my $isok = 0;
my $xml;
my $status = "";
my @status_list = ("CompileError",
		   "WrongAnswer",
		   "TimeLimitExceeded",
		   "MemoryLimitExceeded",
		   "Accepted",
		   "OutputLimitExceeded",
		   "RuntimeError",
		   "PresentationError");

for(my $round = 0; $round < 40; $round++){
    sleep(3);
    my $status_response;
    if(!defined($lesson_id) || $lesson_id eq ""){
	$status_response = $aoj->get('onlinejudge/webservice/status_log',{user_id=>$user});	
    }
    else{
	$status_response = $aoj->get('onlinejudge/webservice/lesson_submission_log',{user_id=>$user,lesson_id=>$lesson_id});
    }

    $xml = $status_response->parse_response();
    
    my $latest_date = str2time($post_response->{_headers}->{date});
    $latest_date = $latest_date . sprintf("0" x (13-length($latest_date)));
    if($xml->{status}[0]->{submission_date} >= $latest_date){

	if(!defined($lesson_id) || $lesson_id eq ""){
	    $status = $xml->{status}[0]->{status};
	}
	else{
	    $status = $status_list[$xml->{status}[0]->{status_code}] . "\n";
	}

	$isok = 1;
	last;
    }
}

print STDERR ($isok ? $status : "Error: Connection Timeout\.n");

__END__

=head1 NAME

aoj - The Simple AOJ Submitter

=head1 SYNOPSIS

Use:

       aoj [options] file

Options:

	-h, --help				show this help message and exit
	-u USERID --user=USERID			set your user_id
	-p PASSWORD --password=PASSWORD		set your password
	-la LANGUAGE --lang=LANGUAGE		set your language
	-i PROBLEM_ID --id=PROBLEM_ID		set your problem_id
	-le LESSON --lesson=LESSON_ID		set your lesson_id

Examples:

	aoj -i 0000 0000_QQ.cpp
	aoj -i A --lang Scala --lesson ITP1_1 ITP1_1_A.scala
